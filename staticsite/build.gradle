buildscript {
    project.ext {
        grainProps = new Properties()
        grainProps.load(new FileInputStream("$project.projectDir/application.properties"))
        grainVersion = grainProps.getProperty('grain.version')

        if (!project.grainVersion) {
            throw new RuntimeException('Grain version is not set in properties file')
        }
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath ('gradle.plugin.org.aim42:htmlSanityCheck:0.9.7')
    }
}

apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'org.aim42.htmlSanityCheck'
apply plugin: 'com.craigburke.client-dependencies'

/**
 * Content changed from grain-theme-business by Patrick Double <pat@patdouble.com>
 */

group = 'com.sysgears.grain'
version = '0.4.2'
defaultTasks 'preview'

ext {
    mainClassName = [project.group, 'Main'].join('.')
    compatibilityVersion = JavaVersion.VERSION_1_8
    assetsVendorDir = clientDependencies.installDir
}

sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

clean.dependsOn clientClean
clean.delete << file('.cache')
clean.delete << file('target')
clean.delete << file('theme/fonts/vendor')
clean.delete << file('theme/javascripts/vendor')
clean.delete << file('theme/sass/vendor')

configurations.all {
}

repositories {
    if (System.getenv('MAVEN_REPO')) {
      maven { url { System.getenv('MAVEN_REPO') } }
    }
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compile "com.sysgears.grain:grain:$project.grainVersion"
    compile 'org.codehaus.groovy:groovy-ant:2.4.10'
    compile 'org.codehaus.groovy:groovy:2.4.10'
}

clientDependencies {
    fileExtensions 'scss'
    npm {
        'bootstrap'('3.3.7')
        'bootstrap-sass'('3.3.7')
        'font-awesome'('4.7.0')
        'jquery'('3.1.1')
    }
}

sourceSets {
    main {
        groovy {
            srcDirs = ['theme/src']
        }
    }
}

project.ext {
    classpath = sourceSets.main.runtimeClasspath
}

idea {
    module {
        excludeDirs = ['.cache', '.idea', '.gradle', '.nb-gradle', '.settings', 'bin', 'out', 'target', 'gradle'].collect { file(it) }
    }
}

task generate(type: JavaExec) {
    logging.captureStandardOutput LogLevel.INFO
    classpath sourceSets.main.runtimeClasspath
    main = mainClassName
    args name
    inputs.files('content','theme','SiteConfig.groovy')
    outputs.file('target')
}

task preview(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = mainClassName
    args name
    jvmArgs "-Xmx1024M", "-XX:MaxPermSize=512M"
}

task copyBootstrap {
    dependsOn clientInstall

    inputs.dir "${assetsVendorDir}/bootstrap"
    inputs.dir "${assetsVendorDir}/bootstrap-sass"

    outputs.dir 'theme/fonts/vendor'
    outputs.dir 'theme/javascripts/vendor'
    outputs.dir 'theme/sass/vendor'

    doLast {
        copy {
            from "${assetsVendorDir}/bootstrap-sass/assets/fonts"
            into 'theme/fonts/vendor'
        }
        copy {
            from "${assetsVendorDir}/jquery/jquery.js"
            into 'theme/javascripts/vendor/jquery/'
        }
        copy {
            from "${assetsVendorDir}/bootstrap/js/bootstrap.js"
            into 'theme/javascripts/vendor/bootstrap/'
        }
        copy {
            from "${assetsVendorDir}/bootstrap-sass/assets/stylesheets"
            into 'theme/sass/vendor/bootstrap'
        }
    }
}

task copyFontawesome {
    dependsOn clientInstall

    inputs.dir "${assetsVendorDir}/font-awesome"

    outputs.dir 'theme/fonts/vendor/fontawesome'
    outputs.dir 'theme/sass/vendor/fontawesome'

    doLast {
        copy {
            from "${assetsVendorDir}/font-awesome/fonts"
            into 'theme/fonts/vendor/fontawesome'
        }
        copy {
            from "${assetsVendorDir}/font-awesome/scss"
            into 'theme/sass/vendor/fontawesome'
            rename 'font-awesome.scss', '_fontawesome.scss'
        }
    }
}

generate.dependsOn copyBootstrap, copyFontawesome
preview.dependsOn copyBootstrap, copyFontawesome

htmlSanityCheck {
    dependsOn generate
    sourceDir = file("target")
    checkingResultsDir = file("${buildDir}/reports/htmlchecks")
    checkExternalLinks = false
}

